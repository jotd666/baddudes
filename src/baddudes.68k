	
	.global		baddudes_reset
	.global		baddudes_irq
	.ifdef		__amiga__
	.include	"baddudes_ram.68k"
	.endif
	.align	2

baddudes_reset:
	move.l	rom_base,a0
	move.l	ram_base,a1
	sub.l	#0xFF8000,a1	| arcade RAM base
	move.l	a1,d1
	
	lea		ram_relocs(pc),a2
0:
	move.l	(a2)+,d0
	jmi		1f
	add.l	d1,(a0,d0.l)
	jra		0b
1:
	lea		patchlist,a0
	move.l	rom_base,a1
	jbsr	apply_patchlist
	* jump to rom start

	move.l	rom_base,-(a7)
	add.l	#0x13dc,(a7)
	rts
	
baddudes_irq:
	move.l	rom_base,-(a7)
	add.l	#0x00384,(a7)
	rts
	
	
	.macro	PL_START
	.endm
	.macro	PL_END
	.word	-1
	.endm
	.macro	PL_I  offset
	.word	0x8000
	.long	\offset
	.endm
	.macro	PL_NOP    offset,nb_nops
	.word	0x8001
	.long	\offset
	.word	\nb_nops
	.endm
	.macro	PL_PS    offset,func
	.word	0x8002
	.long	\offset,\func
	.endm
	.macro	PL_PSS    offset,func,nb_nops
	.word	0x8003
	.long	\offset
	.word	\nb_nops
	.long	\func
	.endm
	.macro	PL_S    offset,skip
	.word	0x8004
	.long	\offset
	.word	\skip
	.endm
	.macro	PL_L    offset,value
	.word	0x8005
	.long	\offset
	.long	\value
	.endm
	.macro	PL_W    offset,value
	.word	0x8006
	.long	\offset
	.word	\value
	.endm
	.macro	PL_B    offset,value
	.word	0x8007
	.long	\offset
	.word	\value
	.endm
	.macro	PL_R    offset
	.word	0x8008
	.long	\offset
	.endm
	.macro	PL_P    offset,func
	.word	0x8009
	.long	\offset,\func
	.endm


* < A0: patchlist
* < A1: address
apply_patchlist:
	movem.l	d0-d7/a2-a6,-(a7)
	lea	patch_table(pc),a2
0:
	move.w	(a0)+,d0
	cmp.w	#-1,d0
	jeq		10f
	and.w	#0xFF,d0
	add.w	d0,d0
	add.w	d0,d0
	move.l	(a0)+,a3
	move.l	(a2,d0.w),a4
	jsr		(a4)
	jra		0b
10:
	jbsr	osd_flush_caches
	movem.l	(a7)+,d0-d7/a2-a6
	rts

patch_table:
	.long	do_PL_I   | offset
	.long	do_PL_NOP | offset,nb_nops
	.long	do_PL_PS  | offset,func
	.long	do_PL_PSS | offset,func,nb_nops
	.long	do_PL_S   | offset,skip
	.long	do_PL_L   | offset,value
	.long	do_PL_W   | offset,value
	.long	do_PL_B   | offset,value
	.long	do_PL_R   | offset
	.long	do_PL_P   | offset,func
	
do_PL_I:
	move.w	#0x4AFC,(a3)
	rts

do_PL_NOP:
	move.w	(a0)+,d0
	lsr.w	#1,d0
	subq	#1,d0
0:
	move.w	#0x4E71,(a3)+
	dbf		d0,0b
	rts
	
do_PL_PS:
	move.w	#0x4EB9,(a3)+
	move.l	(a0)+,(a3)+
	rts
	
do_PL_P:
	move.w	#0x4EF9,(a3)+
	move.l	(a0)+,(a3)+
	rts
	
do_PL_PSS:
	jbsr	do_PL_NOP
	move.w	#0x4EB9,(a3)+
	move.l	(a0)+,(a3)+
	rts
	
do_PL_S:
	move.w	#0x6000,(a3)+
	move.w	(a0)+,(a3)+
	rts
	
do_PL_L:
	move.l	(a0)+,(a3)
	rts
	
do_PL_W:	
	move.w	(a0)+,(a3)
	rts
	
do_PL_B:	
	move.w	(a0)+,d0
	move.b	d0,(a3)
	rts

do_PL_R:
	move.w	#0x4E75,(a3)
	rts
	
	.include	"patchlist.68k"

* manual patches

set_scroll_values:
	STORE_REGS
	exg	a0,a6
	move.l	d0,d7
0:
	jbsr	osd_write_long
	addq	#4,a6
	DBF	D1,0b
	exg	a0,a6
	RESTORE_REGS
	rts
	
enable_interrupts_01476:
	jbsr	osd_enable_interrupts
	rts
	
set_palette_a2:
	STORE_REGS
	exg	a0,a6
0:
	move.l	(a2)+,d7
	jbsr	osd_write_long
	dbf	d0,0b
	
	exg	a0,a6
	RESTORE_REGS
	rts
	
write_word_a0plus_to_0030c010:
	STORE_REGS
	move.w	(a0)+,d7
	lea	0x0030c010,a6
	jbsr	osd_write_word
	RESTORE_REGS
	rts
	
test_input_bit_7:
    *'BTST', 'arguments': ['#7', 'system_inputs_0030c003']
    jbsr    osd_break
    rts

test_input_bit_d1:
    * btst D1,system_inputs_0030c003
    jbsr    osd_break
    nop
    rts

copy_to_tile_064f6:
	ADDA.L	D7,A3
	STORE_REGS
	exg		a6,a0
	MOVE.W	(A3),d7
	jbsr	osd_write_word
	exg		a6,a0
	RESTORE_REGS
	MOVE.L	A0,D3
	rts
	
copy_memory_to_tiles_0837c:
	STORE_REGS
	MOVE.W	#0x1FF,D0
	exg		a6,a1
lb_08380:
	MOVE.w	(A0)+,d7
	jbsr	osd_write_word
	addq.w	#2,a6
	DBF	D0,lb_08380
	exg		a6,a1
	RESTORE_REGS
	RTS
	

clear_sound:
    *;01502: 'address': 5378, 'size': 6, 'instruction': 'CLR.W', 'arguments': ['sound_0030c010']
    rts

test_mcu_reply:
    *'address': 38810, 'size': 6, 'instruction': 'TST.W', 'arguments': ['mcu_reply_0030c008']
    jbsr    osd_break
    nop
    nop
    rts

display_lives_0e122:
	STORE_REGS
	exg	a6,a4
	SUBQ.W	#1,D1	
	BMI.S	lb_0e12e
lb_0e126:
	MOVE.W	#0x0072,d7
	jbsr	osd_write_word
	addq	#2,a6
	DBF	D1,lb_0e126
lb_0e12e:
	moveq	#0,d7
	jbsr	osd_write_byte
lb_0e130:
	exg	a6,a4
	RESTORE_REGS
	RTS
	
write_to_tile_0_083ac:
	STORE_REGS
	move.l	a1,a6
	MOVE.W	D1,D7
	jbsr	osd_write_word
	ADDq.W	#1,D7
	add.w	#64,a6
	jbsr	osd_write_word
	ADDq.W	#1,D7
	sub.w	#66,a6
	jbsr	osd_write_word
	ADDq.W	#1,D7
	add.w	#64,a6
	jbsr	osd_write_word
	RESTORE_REGS
	rts
	
set_video_attribute_083ce:
	STORE_REGS
	LEA	video_ram_244000+0x100,A0
	lea	0x244100,a6
lb_083d4:
	MOVE.W	(A0)+,D7		
	ANDI.W	#0x0fff,D7
	ORI.W	#0x3000,D7
	jbsr	osd_write_word
	addq	#2,a6
	CMPA.L	#0x244300,A6
	BNE.S	lb_083d4
	RESTORE_REGS
	rts

write_ninja_message_0834A:
	ANDI.W	#0x00ff,D0
	exg		a1,a6
	STORE_REGS
	move.w	d0,d7
	jbsr	osd_write_word
	RESTORE_REGS
	exg		a1,a6
	rts
	
copy_to_tile_0_01fa2:
	STORE_REGS
	exg		a6,a1
lb_01fa2:
	CMPI.B	#0xfe,(A0)		
	BNE.W	lb_01fba		
	ADDA.W	#0x40,A2	
	MOVEA.L	A2,A6			
	ADDQ	#1,A0	
	BRA.S	lb_01fa2		
lb_01fba:
	MOVE.B	D1,d7
	jbsr	osd_write_byte
	addq	#1,a6
	MOVE.B	(A0)+,d7		
	jbsr	osd_write_byte
	addq	#1,a6
	DBF	D0,lb_01fa2
	exg		a6,a1
	RESTORE_REGS
	RTS				
	
copy_to_palette_007be:
	STORE_REGS
	exg	a0,a6
0:
	MOVE.W	#0x000f,D1
1:
	MOVE.W	(A1)+,d7
	jbsr	osd_write_word
	DBF	D1,1b
	DBF	D0,0b
	exg	a0,a6
	RESTORE_REGS
	RTS
	
copy_rom_to_video_1b310:
	move.w	d7,-(a7)
	exg		a6,a1
0:
    MOVE.W    (A2)+,d7
	jbsr	osd_write_byte
    SUBQ.W    #1,D1            
    BCC.S    0b
    SUBQ.W    #1,D0
    BCS.S    lb_1b320
    MOVE.W    #0x000f,D1
    BRA.S    0b
lb_1b320:
	exg		a6,a1
	move.w	(a7)+,d7
    RTS
	
